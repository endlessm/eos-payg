subdir('plugins')

deps = [
  dependency('gio-2.0', version: '>= 2.44'),
  dependency('glib-2.0', version: '>= 2.44'),
  dependency('gobject-2.0', version: '>= 2.44'),
  libgsystemservice_dep,
  libeos_payg_dep,
]

envs = test_env + [
  'G_TEST_SRCDIR=' + meson.current_source_dir(),
  'G_TEST_BUILDDIR=' + meson.current_build_dir(),
]

test_programs = {
  'manager' : {},
  'multi-task' : {},
  'service' : {},
  'provider-loader' : {},
  'boottime-source' : {},
  'clock-jump-source' : {'suites': ['unsafe']},
}

installed_tests_metadir = join_paths(datadir, 'installed-tests',
                                     'libeos-payg-' + eos_payg_api_version)
installed_tests_execdir = join_paths(libexecdir, 'installed-tests',
                                     'libeos-payg-' + eos_payg_api_version)

foreach program_name, extra_args : test_programs
  test_conf = configuration_data()
  test_conf.set('installed_tests_dir', installed_tests_execdir)
  test_conf.set('program', program_name)

  configure_file(
    input: test_template,
    output: program_name + '.test',
    install: enable_installed_tests,
    install_dir: installed_tests_metadir,
    configuration: test_conf,
  )

  exe = executable(
    program_name,
    [program_name + '.c'],
    dependencies: deps,
    include_directories: root_inc,
    install: enable_installed_tests,
    install_dir: installed_tests_execdir,
    install_rpath: pkglibdir,
  )

  test(
    program[0],
    exe,
    env: envs,
    suite: ['eos-payg'] + extra_args.get('suites', []),
  )
endforeach
